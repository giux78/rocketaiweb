@echo off

PUSHD "%~dp0"
setlocal

call .venv\Scripts\activate.bat
set INVOKEAI_ROOT=.

echo Do you want to generate images using the
echo 1. command-line
echo 2. browser-based UI
echo 3. run textual inversion training
echo 4. open the developer console
echo 5. re-run the configure script to download new models
set /P restore="Please enter 1, 2, 3, 4 or 5: [5] "
if not defined restore set restore=2
IF /I "%restore%" == "1" (
    echo Starting the InvokeAI command-line..
    python .venv\Scripts\invoke.py %*
) ELSE IF /I "%restore%" == "2" (
    echo Starting the InvokeAI browser-based UI..
    python .venv\Scripts\invoke.py --web %*
) ELSE IF /I "%restore%" == "3" (
    echo Starting textual inversion training..
    python .venv\Scripts\textual_inversion_fe.py --web %*
) ELSE IF /I "%restore%" == "4" (
    echo Developer Console
    echo Python command is:
    where python
    echo Python version is:
    python --version
    echo *************************
    echo You are now in the system shell, with the local InvokeAI Python virtual environment activated,
    echo so that you can troubleshoot this InvokeAI installation as necessary.
    echo *************************
    echo *** Type `exit` to quit this shell and deactivate the Python virtual environment ***
    call cmd /k
) ELSE IF /I "%restore%" == "5" (
    echo Running configure_invokeai.py...
    python .venv\Scripts\configure_invokeai.py --web %*
) ELSE (
    echo Invalid selection
    pause
    exit /b
)
endlocal
